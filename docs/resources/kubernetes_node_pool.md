---
page_title: "civo_kubernetes_node_pool Resource - terraform-provider-civo"
subcategory: ""
description: |-
  Provides a Civo Kubernetes node pool resource. While the default node pool must be defined in the civo_kubernetes_cluster resource, this resource can be used to add additional ones to a cluster.
---

# civo_kubernetes_node_pool (Resource)

Provides a Civo Kubernetes node pool resource. While the default node pool must be defined in the `civo_kubernetes_cluster` resource, this resource can be used to add additional ones to a cluster.

## Example Usage

```terraform
# Query xsmall instance size
data "civo_size" "xsmall" {
    filter {
        key = "type"
        values = ["kubernetes"]
    }

    sort {
        key = "ram"
        direction = "asc"
    }
}

# Create a cluster
resource "civo_kubernetes_cluster" "my-cluster" {
    name = "my-cluster"
    applications = "Portainer,Linkerd:Linkerd & Jaeger"
    firewall_id = civo_firewall.my-firewall.id
    node_pool {
        size = element(data.civo_size.xsmall.sizes, 0).name
        node_count = 3
    }
}

# Add a node pool
resource "civo_kubernetes_node_pool" "back-end" {
   cluster_id = civo_kubernetes_cluster.my-cluster.id
   label = "back-end" // Optional
   node_count = 1 // Optional
   size = element(data.civo_size.xsmall.sizes, 0).name // Optional
   region = "LON1"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster_id` (String) The ID of your cluster
- `region` (String) The region of the node pool, has to match that of the cluster

### Optional

- `label` (String) Node pool label, if you don't provide one, we will generate one for you
- `labels` (Map of String)
- `node_count` (Number) the number of instances to create (optional, the default at the time of writing is 3)
- `public_ip_node_pool` (Boolean) Node pool belongs to the public ip node pool
- `size` (String) the size of each node (optional, the default is currently g4s.kube.medium)
- `taint` (Block Set) (see [below for nested schema](#nestedblock--taint))
- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `id` (String) The ID of this resource.
- `instance_names` (List of String) Instance names in the nodepool

<a id="nestedblock--taint"></a>
### Nested Schema for `taint`

Required:

- `effect` (String)
- `key` (String)
- `value` (String)


<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)
- `delete` (String)
- `update` (String)

## Import

Import is supported using the following syntax:

```shell
terraform import civo_kubernetes_node_pool.my-pool 1b8b2100-0e9f-4e8f-ad78-9eb578c2a0af:502c1130-cb9b-4a88-b6d2-307bd96d946a
```
## Taint and Labels

The Kubernetes node pool resource supports taints and labels. These can be specified as a map of key/value pairs. For example:

```terraform
# Query xsmall instance size
data "civo_size" "xsmall" {
    filter {
        key = "type"
        values = ["kubernetes"]
    }

    sort {
        key = "ram"
        direction = "asc"
    }
}

# Create a cluster
resource "civo_kubernetes_cluster" "my-cluster" {
    name = "my-cluster"
    applications = "Portainer,Linkerd:Linkerd & Jaeger"
    firewall_id = civo_firewall.my-firewall.id
    
    node_pool {
        size = element(data.civo_size.xsmall.sizes, 0).name
        node_count = 3
    }
}

# Add a node pool
resource "civo_kubernetes_node_pool" "back-end" {
   cluster_id = civo_kubernetes_cluster.my-cluster.id
   label = "back-end" // Optional
   node_count = 1 // Optional
   size = element(data.civo_size.xsmall.sizes, 0).name // Optional
   region = "LON1"

   labels = {
     service  = "backend"
     priority = "high"
   }

  taint {
    key    = "workloadKind"
    value  = "database"
    effect = "NoSchedule"
  }

  taint {
    key    = "workloadKind"
    value  = "frontend"
    effect = "NoSchedule"
  }
}
```

### Taints

Taints are used to repel pods from nodes. They can be used to repel pods from nodes with certain hardware, or to repel pods from nodes that are running certain services. For example, you may want to repel pods from nodes that are running a database, or from nodes that are running a monitoring agent.
Taints have to currently be removed from nodes using the kubectl command in addition to being removed from a Terraform configuration. For example:

```shell
kubectl taint nodes node-1 key=value:NoSchedule-
```
This will be automated in a future release of the provider. Removing a taint from Terraform will prevent the node from being tainted again if node pools with the taints are altered or scaled.
